plugins {
    id 'org.jetbrains.intellij' version '1.16.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    // For the kotlin version, check the support offered by the platform version:
    // https://plugins.jetbrains.com/docs/intellij/using-kotlin.html#kotlin-standard-library
}

group 'io.github.stefansjs.flatbuffersplugin'
version "0.5+${getBuildNumber()}"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test-testng:1.6.21'
}

intellij {
    plugins = ['com.intellij.java']
    version = '2022.2'
    localPath = findProperty('io.github.stefansjs.flatbuffersplugin.idePath')
    downloadSources = true
    updateSinceUntilBuild = false
}

patchPluginXml {
    sinceBuild = '222'
}

sourceSets.main.java.srcDir 'src/main/gen'

test {
}

runIde.jvmArgs += "-XX:+UnlockDiagnosticVMOptions"

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

String getBuildNumber() {
    if(System.getenv("GITHUB_RUN_NUMBER") != null) {
        System.getenv("GITHUB_RUN_NUMBER")
    } else {
        "${getGitHash()}"
    }
}
