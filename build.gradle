plugins {
    id 'org.jetbrains.intellij' version '0.6.5'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    // For the kotlin version, check the support offered by the platform version:
    // https://plugins.jetbrains.com/docs/intellij/using-kotlin.html#kotlin-standard-library
}

group 'io.github.stefansjs.flatbuffersplugin'
version "0.4-beta+${getBuildNumber()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-testng"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    plugins = ['com.intellij.java']
    version = '2020.2'
    alternativeIdePath = findProperty('io.github.stefansjs.flatbuffersplugin.idePath')
    downloadSources = true
    updateSinceUntilBuild = false
}

sourceSets.main.java.srcDir 'src/main/gen'

test {
}

runIde.jvmArgs += "-XX:+UnlockDiagnosticVMOptions"

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

String getBuildNumber() {
    if(System.getenv("GITHUB_RUN_NUMBER") != null) {
        System.getenv("GITHUB_RUN_NUMBER")
    } else {
        "${getGitHash()}"
    }
}
